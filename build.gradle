plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.20'
    id 'application'
    id 'org.flywaydb.flyway' version '8.5.1'
}

group "com.todo.example"
version "0.0.1"
mainClassName = "com.todo.example.ApplicationKt"

def isDevelopment = project.ext.has("development")
applicationDefaultJvmArgs = ["-Dio.ktor.development=$isDevelopment"]

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-tomcat:$ktor_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "org.jetbrains.exposed:exposed:0.17.14"
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation "org.postgresql:postgresql:42.3.3"
    implementation 'org.flywaydb:flyway-core:8.5.1'
    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

jar {
    manifest {
        attributes 'Main-Class': "com.todo.example.ApplicationKt"
    }
    from {
        configurations.runtimeClasspath.collect() {
            it.isDirectory() ? it : zipTree(it)
        }
    }

}

task stage {
    dependsOn installDist
}

def JDBC_DATABASE_URL = System.getenv('JDBC_DATABASE_URL') == null
        ? "jdbc:postgresql://localhost:5432/todo"
        : System.getenv('JDBC_DATABASE_URL')

def JDBC_DATABASE_USERNAME = System.getenv('JDBC_DATABASE_USERNAME') == null
        ? "ktoruser"
        : System.getenv('JDBC_DATABASE_USERNAME')
def JDBC_DATABASE_PASSWORD = System.getenv('JDBC_DATABASE_PASSWORD') == null
        ? "ktorpass"
        : System.getenv('JDBC_DATABASE_PASSWORD')

flyway {
    url = JDBC_DATABASE_URL
    user = JDBC_DATABASE_USERNAME
    password = JDBC_DATABASE_PASSWORD
    baselineOnMigrate = true
    locations = ["filesystem:resources/db/migration"]
}